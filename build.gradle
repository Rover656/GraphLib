
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "1.1.1"
group= "gigaherz.graph" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "GraphLib"

minecraft {
    version = "1.9.4-12.17.0.1976"
    runDir = "run"

    mappings = "snapshot_20160518"

    replace "@VERSION@", project.version
    replaceIn "Constants.java"
}

sourceSets {
    debug {
        java {
            srcDir 'src/debug/java'
        }
        resources {
            srcDir 'src/debug/resources'
        }
        compileClasspath += main.output
    }
    main {
        runtimeClasspath += debug.output
    }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

