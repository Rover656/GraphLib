buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {url "http://tehnut.info/maven"}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "2.0.0"
group= "gigaherz.graph" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "GraphLib2"

minecraft {
    version = "1.12.2-14.23.4.2707"
    runDir = "run"

    mappings = "snapshot_20180601"

    replace "@VERSION@", project.version
    replaceIn "Constants.java"
}

dependencies {
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
}

jar {
    manifest {
        attributes([
            "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}",
            "Timestamp": System.currentTimeMillis()
        ])
    }
}

sourceSets {
    debug {
        java {
            srcDir 'src/debug/java'
        }
        resources {
            srcDir 'src/debug/resources'
        }
        compileClasspath += main.output
        compileClasspath += main.compileClasspath
    }
    main {
        runtimeClasspath += debug.output
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}

